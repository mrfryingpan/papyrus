apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'

group = 'com.github.mrfryingpan.papyrus'

buildscript {
    ext.kotlin_version = '1.5.20'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.15.2"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

}
configure(subprojects.findAll {!it.name.contains('app')}) {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId = rootProject.group
                artifactId = "${project.getName()}"
                version = "1.5.2"
                // Tell maven to prepare the generated "*.aar" file for publishing
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

                pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    System.out.println(it.group)

                    if (it.group == 'Papyrus 2') {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', rootProject.group)
                        dependency.appendNode('artifactId', "${it.name}")
                        dependency.appendNode('version', version)
                    } else {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                    }
                }
            }
            }
        }
    }

    artifactoryPublish {
        publications(publishing.publications.aar)
    }
}

ext {
    groupID = 'com.papyrus'
    versionName = "1.5.1"
    versionCode = 11

    minSdkVersion = 16
    targetSdkVersion = 29
    compileSdkVersion = 29
    buildToolsVersion = "28.0.3"

    //LibraryVersions
    AppCompat = "1.2.0"
    AppCompatAnnotations = "1.1.0"
    AppCompatRecycler = "1.1.0"
    SwipeRefresh = "1.1.0"
    Material = "1.2.1"
    Support = "28+"
    JUnit = "4.12"
    TestRunner = "1.3.0"
    Espresso = "3.3.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

artifactoryPublish.skip = true
project("app"){
    artifactoryPublish.skip = true
}
artifactory {
    contextUrl = "https://papyrus.jfrog.io/artifactory/"
    publish {
        repository {
            repoKey = "papyrus"
            username = System.getenv("JFROG_USER")
            password = System.getenv("JFROG_PASS")
            maven = true
        }
        defaults {
            publishArtifacts = true
            publishPom = true
        }
    }
}

